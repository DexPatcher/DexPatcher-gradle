/*
 * DexPatcher - Copyright 2015-2020 Rodrigo Balerdi
 * (GNU General Public License version 3 or later)
 *
 * DexPatcher is free software: you can redistribute it and/or modify
 * it under the terms of the GNU General Public License as published
 * by the Free Software Foundation, either version 3 of the License,
 * or (at your option) any later version.
 */

plugins {
    id 'groovy'
    id 'java-gradle-plugin'
    id 'com.gradle.plugin-publish' version '0.12.0'
}

group = 'com.github.lanchon.dexpatcher'
version = '2.0.0'

ext.mainArtifact = 'dexpatcher-gradle'
ext.artifactName = 'DexPatcher-gradle'

repositories {
    jcenter()
    google()
}

dependencies {
    implementation localGroovy()
    compileOnly 'com.android.tools.build:gradle:3.4.0'
}

compileGroovy {
    options.compilerArgs << '-proc:none'
}

apply from: 'configure-artifacts.gradle'

afterEvaluate {
    def archivesArtifacts = configurations.archives.artifacts
    archivesArtifacts.removeAll archivesArtifacts.findAll { it.classifier in ['javadoc', 'groovydoc'] }
}

gradlePlugin {

    def idPrefix = 'com.github.lanchon.dexpatcher.'
    def classPrefix = 'lanchon.dexpatcher.gradle.plugins.'

    plugins {

        basePlugin {
            id = idPrefix + 'base'
            implementationClass = classPrefix + 'DexpatcherBasePlugin'
        }

        apkLibraryPlugin {
            id = idPrefix + 'apk-library'
            implementationClass = classPrefix + 'ApkLibraryPlugin'
        }

        patchedAppPlugin {
            id = idPrefix + 'patched-application'
            implementationClass = classPrefix + 'PatchedAppPlugin'
        }

        patchLibraryPlugin {
            id = idPrefix + 'patch-library'
            implementationClass = classPrefix + 'PatchLibraryPlugin'
        }

    }

}

pluginBundle {

    def descriptionPrefix = 'DexPatcher is a toolchain for modifying Android applications at source-level using Java ' +
            'and the full power of Android Studio. '
    def descriptionSuffix = 'DexPatcher is free software. (GPLv3+)'

    website = 'https://dexpatcher.github.io/'
    vcsUrl = 'https://github.com/DexPatcher/DexPatcher-gradle'

    description = descriptionPrefix + descriptionSuffix

    tags = ['dexpatcher', 'apktool', 'dex2jar', 'patch', 'modify', 'mod', 'reverse', 'engineer', 'reengineer',
            'android', 'application', 'apk', 'dalvik', 'bytecode']

    plugins {

        basePlugin {
            displayName = 'DexPatcher Base'
            description = descriptionPrefix +
                    'The DexPatcher Base plugin provides basic DexPatcher, Apktool and dex2jar tasks. ' +
                    descriptionSuffix
        }

        apkLibraryPlugin {
            displayName = 'DexPatcher APK Library'
            description = descriptionPrefix +
                    'The DexPatcher APK Library plugin processes a source application provided as an Android APK ' +
                    'and produces a DexPatcher APK library. The library is used to create modified versions of the ' +
                    'source application and includes its Dalvik bytecode and decoded manifest and resources. ' +
                    descriptionSuffix
        }

        patchedAppPlugin {
            displayName = 'DexPatcher Patched Application'
            description = descriptionPrefix +
                    'The DexPatcher Patched Application plugin produces a modified Android application by applying ' +
                    'changes to code, manifest and resources of a source application provided as an Android APK or ' +
                    'a DexPatcher APK library. ' +
                    descriptionSuffix
        }

        patchLibraryPlugin {
            displayName = 'DexPatcher Patch Library'
            description = descriptionPrefix +
                    'The DexPatcher Patch Library plugin produces a DexPatcher patch library that bundles compiled ' +
                    'changes to code, manifest and resources of a source application without bundling any part of '  +
                    'the application itself. The source application is provided as an Android APK or a DexPatcher ' +
                    'APK library. DexPatcher patch libraries are similar in structure to Android libraries (AAR ' +
                    'files). The changes bundled in a patch library are applied to its source application using the ' +
                    'DexPatcher Patched Application plugin. Several patch libraries can be applied in the same step ' +
                    'or their application can be serialized and chained. ' +
                    descriptionSuffix
        }

    }

    mavenCoordinates {
        groupId = group
        artifactId = mainArtifact
    }

}

apply from: 'check-copyright.gradle'

wrapper.distributionType = Wrapper.DistributionType.ALL
